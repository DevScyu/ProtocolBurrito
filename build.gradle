plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version "$kotlin_version"
    id 'org.jetbrains.kotlin.kapt' version "$kotlin_version"
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'io.github.slimjar' version "1.2.1"
}

apply from: 'https://raw.githubusercontent.com/MineInAbyss/shared-gradle/83bfbc4a243a34fe53c6ff709ea1ff72f2de6b07/common.gradle'

allprojects {
    apply plugin: 'java'

    group = 'com.mineinabyss'
    version = plugin_version

    repositories {
        mavenCentral()
        maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots" }
        maven { url "https://repo.dmulloy2.net/nexus/repository/public/" } //ProtocolLib
        maven { url "https://repo.mineinabyss.com/releases/" }
//        maven { url "https://repo.codemc.io/repository/maven-releases" }
    }

    dependencies {
        compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        compileOnly "org.spigotmc:spigot-api:$server_version" //Spigot
        compileOnly ("com.comphenix.protocol:ProtocolLib:4.5.0") {
            // this dep wasn't being resolved.
            exclude group: 'com.comphenix.executors', module: 'BukkitExecutors'
        }
    }
}

dependencies {
    slim "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "com.mineinabyss:idofront:1.17.1-0.6.23"
    implementation "io.github.slimjar:slimjar:1.2.4"
    implementation project(":shared")
    compileOnly project(":generator")
    kapt project(":generator")
}

compileKotlin { kotlinOptions.jvmTarget = '1.8' }
compileTestKotlin { kotlinOptions.jvmTarget = '1.8' }

build.dependsOn shadowJar

task sourceJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
    from "$buildDir/generated/source/kaptKotlin/main"
}

publishing {
    repositories {
        maven {
            url = "https://repo.mineinabyss.com/releases"
            credentials {
                username = findProperty('mineinabyssMavenUsername')
                password = findProperty('mineinabyssMavenPassword')
            }
        }
    }
    publications {
        maven(MavenPublication) { publication ->
            artifactId = "protocolburrito"
            project.shadow.component(publication)

            artifact sourceJar
        }
    }
}
