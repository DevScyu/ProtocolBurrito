plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version "$kotlin_version"
    id 'org.jetbrains.kotlin.kapt' version '1.4.20'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'io.github.0ffz.github-packages' version '1.2.0'
    id 'me.bristermitten.pdm' version '0.0.32'
}

apply from: 'https://raw.githubusercontent.com/MineInAbyss/shared-gradle/master/common.gradle'

allprojects {
    apply plugin: 'java'

    group = 'com.mineinabyss'
    version = plugin_version

    repositories {
        mavenCentral()
        maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots" }
        maven { url "https://repo.dmulloy2.net/nexus/repository/public/" } //ProtocolLib
//        maven { url "https://repo.codemc.io/repository/maven-releases" }
    }

    dependencies {
        compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        compileOnly "org.spigotmc:spigot-api:$server_version" //Spigot
        compileOnly ("com.comphenix.protocol:ProtocolLib:4.5.0") {
            // this dep wasn't being resolved.
            exclude group: 'com.comphenix.executors', module: 'BukkitExecutors'
        }
    }

}

dependencies {
    pdm "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation project(":shared")
    compileOnly project(":generator")
    kapt project(":generator")
}

compileKotlin { kotlinOptions.jvmTarget = '1.8' }
compileTestKotlin { kotlinOptions.jvmTarget = '1.8' }

shadowJar {
    classifier ""
}
build.dependsOn shadowJar
shadowJar.dependsOn project.tasks.getByName('pdm')

task sourceJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
    from "$buildDir/generated/source/kaptKotlin/main"
}

publishing {
    repositories {
        maven githubPackagePublish.invoke("MineInAbyss/ProtocolBurrito")
    }
    publications {
        gpr(MavenPublication) { publication ->
            artifactId = "protocolburrito"
            project.shadow.component(publication)

            artifact sourceJar
        }
    }
}
